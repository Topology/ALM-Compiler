system description monkeyBanana
	theory basic_motion
		module moving
			sort declarations
				points, things :: universe
				agents :: things
				move :: actions
					attributes
						actor : agents
						origin : points
						dest : points
			function declarations
				statics
					basic
						symmetric_connectivity : booleans
						transitive_connectivity : booleans
				fluents
					basic
						connected : points * points -> booleans
						total loc_in : things -> points
			axioms
				dynamic causal laws
					occurs(X) causes loc_in(A) = D if instance(X, move),actor(X) = A,dest(X) = D.
				state constraints
					connected(X, X).
					connected(X, Y) if connected(Y, X),symmetric_connectivity.
					-connected(X, Y) if -connected(Y, X),symmetric_connectivity.
					connected(X, Z) if connected(X, Y),connected(Y, Z),transitive_connectivity.
				executability conditions
					impossible occurs(X) if instance(X, move),actor(X) = A,loc_in(A) != origin(X).
					impossible occurs(X) if instance(X, move),actor(X) = A,loc_in(A) = dest(X).
					impossible occurs(X) if instance(X, move),actor(X) = A,loc_in(A) = O,dest(X) = D,-connected(O, D).
structure bob_and_john
	instances
		bob, john in agents
		new_york, paris, rome in points
		go(X,P1,P2) in move where P1 != P2
			actor = X
			origin = P1
			dest = P2
		go(X,P) in move
			actor = X
			dest = P
	value of statics
		symmetric_connectivity.
		-transitive_connectivity.
