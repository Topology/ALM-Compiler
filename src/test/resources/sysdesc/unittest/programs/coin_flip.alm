system description coin_flip
theory coin_flip
module coin_flip
	sort declarations
		sides :: universe
		coins :: universe
		flips :: actions
			attributes
				flipped_coin : coins
		hides :: actions
			attributes
				coin_hidden : coins
	constant declarations
		heads, tails : sides
	function declarations
		fluents 
			basic
				showing: coins -> sides
	axioms
		dynamic causal laws
			occurs(X) causes showing(Y) = heads if instance(X, flips), flipped_coin(X) = Y, showing(Y) = tails.
			occurs(X) causes showing(Y) = tails if instance(X, flips), flipped_coin(X) = Y, showing(Y) = heads.
			occurs(X) causes -dom_showing(Y) if instance(X, hides), coin_hidden(X) = Y. 
structure coin_flip
	instances
		nickel, dime, quarter in coins
		flip(X) in flips
			flipped_coin = X
		hide(X) in hides
			coin_hidden = X
    constant definitions
        heads = front
        tails = back
temporal projection
max steps 6
history 
observed(showing(dime), heads, 0).
happened(flip(dime), 0).
observed(showing(nickel), tails, 0).
happened(flip(nickel), 0).
%%%% Hiding a coin makes the side shown undefined.  
happened(hide(nickel), 1).
happened(flip(dime), 2).
happened(flip(nickel), 2).
observed(showing(nickel), heads, 3).
happened(flip(dime), 4).
happened(flip(nickel), 4).
%End of File